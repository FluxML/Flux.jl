# [Deep Learning with Julia & Flux: A 60 Minute Blitz](@id man-blitz)

This is a quick intro to [Flux](https://github.com/FluxML/Flux.jl) loosely based on [PyTorch's tutorial](https://pytorch.org/tutorials/beginner/deep_learning_60min_blitz.html). It introduces basic Julia programming, as well Zygote, a source-to-source automatic differentiation (AD) framework in Julia. We'll use these tools to build a very simple neural network.

## Arrays

The starting point for all of our models is the `Array` (sometimes referred to as a `Tensor` in other frameworks). This is really just a list of numbers, which might be arranged into a shape like a square. Let's write down an array with three elements.

```julia
x = [1, 2, 3]
```

Here's a matrix – a square array with four elements.

```julia
x = [1 2; 3 4]
```

We often work with arrays of thousands of elements, and don't usually write them down by hand. Here's how we can create an array of 5×3 = 15 elements, each a random number from zero to one.

```julia
x = rand(5, 3)
```

There's a few functions like this; try replacing `rand` with `ones`, `zeros`, or `randn` to see what they do.

By default, Julia works stores numbers is a high-precision format called `Float64`. In ML we often don't need all those digits, and can ask Julia to work with `Float32` instead. We can even ask for more digits using `BigFloat`.

```julia
x = rand(BigFloat, 5, 3)

x = rand(Float32, 5, 3)
```

We can ask the array how many elements it has.

```julia
length(x)
```

Or, more specifically, what size it has.

```julia
size(x)
```

We sometimes want to see some elements of the array on their own.

```julia
x

x[2, 3]
```

This means get the second row and the third column. We can also get every row of the third column.

```julia
x[:, 3]
```

We can add arrays, and subtract them, which adds or subtracts each element of the array.

```julia
x + x

x - x
```

Julia supports a feature called *broadcasting*, using the `.` syntax. This tiles small arrays (or single numbers) to fill bigger ones.

```julia
x .+ 1
```

We can see Julia tile the column vector `1:5` across all rows of the larger array.

```julia
zeros(5,5) .+ (1:5)
```

The `x'` syntax is used to transpose a column `1:5` into an equivalent row, and Julia will tile that across columns.

```julia
zeros(5,5) .+ (1:5)'
```

We can use this to make a times table.

```julia
(1:5) .* (1:5)'
```

Finally, and importantly for machine learning, we can conveniently do things like matrix multiply.

```julia
W = randn(5, 10)
x = rand(10)
W * x
```

Julia's arrays are very powerful, and you can learn more about what they can do [here](https://docs.julialang.org/en/v1/manual/arrays/).

### CUDA Arrays

CUDA functionality is provided separately by the [CUDA package](https://github.com/JuliaGPU/CUDA.jl). If you have a GPU and CUDA available, you can run `] add CUDA` in a REPL or IJulia to get it.

Once CUDA is loaded you can move any array to the GPU with the `cu` function, and it supports all of the above operations with the same syntax.

```julia
using CUDA
x = cu(rand(5, 3))
```

## Automatic Differentiation

You probably learned to take derivatives in school. We start with a simple mathematical function like

```julia
f(x) = 3x^2 + 2x + 1

f(5)
```

In simple cases it's pretty easy to work out the gradient by hand – here it's `6x+2`. But it's much easier to make Flux do the work for us!

```julia
using Flux: gradient

df(x) = gradient(f, x)[1]

df(5)
```

You can try this with a few different inputs to make sure it's really the same as `6x+2`. We can even do this multiple times (but the second derivative is a fairly boring `6`).

```julia
ddf(x) = gradient(df, x)[1]

ddf(5)
```

Flux's AD can handle any Julia code you throw at it, including loops, recursion and custom layers, so long as the mathematical functions you call are differentiable. For example, we can differentiate a Taylor approximation to the `sin` function.

```julia
mysin(x) = sum((-1)^k*x^(1+2k)/factorial(1+2k) for k in 0:5)

x = 0.5

mysin(x), gradient(mysin, x)

sin(x), cos(x)
```

You can see that the derivative we calculated is very close to `cos(x)`, as we expect.

This gets more interesting when we consider functions that take *arrays* as inputs, rather than just a single number. For example, here's a function that takes a matrix and two vectors (the definition itself is arbitrary)

```julia
myloss(W, b, x) = sum(W * x .+ b)

W = randn(3, 5)
b = zeros(3)
x = rand(5)

gradient(myloss, W, b, x)
```

Now we get gradients for each of the inputs `W`, `b` and `x`, which will come in handy when we want to train models.

ML models can contain hundreds of parameter arrays, therefore it is handy to group them into **layers**.
A layer is just a handy container for some parameters. For example, `Dense` does a linear transform for you.

```julia
using Flux

m = Dense(10 =>  5)

x = rand(Float32, 10)
```

We can easily get the parameters of any layer or model with `trainables`.

```julia
Flux.trainables(m)
```

It very easy to calculate the gradient for all parameters in a network, even if it has many parameters.
The function `gradient` is not limited to array but can compute the gradient with respect to generic composite types.

```julia
using Flux
using Flux: logitcrossentropy, trainables, getkeypath

x = rand(Float32, 10)
model = Chain(Dense(10 => 5, relu), Dense(5 => 2))
loss(model, x) = logitcrossentropy(model(x), [0.5, 0.5])
grad = gradient(m -> loss(m, x), model)[1]
for (k, p) in trainables(model, path=true)
    println("$k  => $(getkeypath(grad, k))")
end
```

You don't have to use layers, but they can be convient for many simple kinds of models and fast iteration.

The next step is to update our weights and perform optimisation. As you might be familiar, *Gradient Descent* is a simple algorithm that takes the weights and steps using a learning rate and the gradients. `weights = weights - learning_rate * gradient`.

```julia
η = 0.1
for (k, p) in trainables(model, path=true)
    p .+= -η * getkeypath(grad, p)
end
```

While this is a valid way of updating our weights, it can get more complicated as the algorithms we use get more involved.

Flux comes with a bunch of pre-defined optimisers and makes writing our own really simple. We just give it the learning rate `η`:

```julia
opt_state = Flux.setup(Descent(η), model)
```

Training a network reduces down to iterating on a dataset multiple times, performing these steps in order. Just for a quick implementation, let’s train a network that learns to predict `0.5` for every input of 10 floats. `Flux` defines the `train!` function to do it for us.

```julia
data, labels = rand(10, 100), fill(0.5, 2, 100)
loss(m, x, y) = logitcrossentropy(m(x), y)
Flux.train!(loss, model, [(data, labels)], opt_state)
```

You don't have to use `train!`. In cases where arbitrary logic might be better suited, you could open up this training loop like so:

```julia
for d in training_set # assuming d looks like (data, labels)
    # our super logic
    g = gradient(model) do model
        l = loss(model, d...)
    end[1]
    Flux.update!(opt_state, model, g)
end
```

The `do` block is a closure, which is a way of defining a function inline. It's a very powerful feature of Julia, and you can learn more about it [here](https://docs.julialang.org/en/v1/manual/functions/#Do-Block-Syntax-for-Function-Arguments).

## Training a Classifier

Getting a real classifier to work might help cement the workflow a bit more. [CIFAR10](https://https://www.cs.toronto.edu/~kriz/cifar.html) is a dataset of 50k tiny training images split into 10 classes.

We will do the following steps in order:

* Load CIFAR10 training and test datasets
* Define a Convolution Neural Network
* Define a loss function
* Train the network on the training data
* Test the network on the test data

### Loading the Dataset

```julia
using Statistics
using Flux
using MLDatasets: CIFAR10
using ImageCore: colorview, RGB
using Flux: onehotbatch, onecold, DataLoader
using Plots: plot
using MLUtils: splitobs, numobs

# using CUDA # Uncomment if you have CUDA installed. Can also use AMDGPU or Metal instead
# using AMDGPU
# using Metal
```

This image will give us an idea of what we are dealing with. 

![title](https://pytorch.org/tutorials/_images/cifar10.png)

```julia
train_x, train_y = CIFAR10(:train)[:]
labels = onehotbatch(train_y, 0:9)
```

The `train_x` contains 50000 images converted to 32 X 32 X 3 arrays with the third dimension being the 3 channels (R,G,B). Let's take a look at a random image from the train_x. For this, we need to permute the dimensions to 3 X 32 X 32 and use `colorview` to convert it back to an image. 

```julia
image(x) = colorview(RGB, permutedims(x, (3, 2, 1)))
plot(image(train_x[:,:,:,rand(1:end)]))
```

We can now arrange the training data in batches of say, 256 and keep a validation set to track our progress. This process is called minibatch learning, which is a popular method of training large neural networks. Rather that sending the entire dataset at once, we break it down into smaller chunks (called minibatches) that are typically chosen at random, and train only on them. It is shown to help with escaping [saddle points](https://en.wikipedia.org/wiki/Saddle_point).

The first 45k images (in batches of 256) will be our training set, and the rest is for validation. 
The `DataLoader` function will help us load the data in batches.

```julia
trainset, valset = splitobs((train_x, labels), at = 45000)
trainloader = DataLoader(trainset, batchsize = 1000, shuffle = true)
valloader = DataLoader(trainset, batchsize = 1000)
```

### Defining the Classifier

Now we can define our Convolutional Neural Network (CNN).

A convolutional neural network is one which defines a kernel and slides it across a matrix to create an intermediate representation to extract features from. It creates higher order features as it goes into deeper layers, making it suitable for images, where the strucure of the subject is what will help us determine which class it belongs to.

```julia
model = Chain(
          Conv((5,5), 3 => 16, relu),
          MaxPool((2, 2)),
          Conv((5, 5), 16 => 8, relu),
          MaxPool((2,2)),
          x -> reshape(x, :, size(x, 4)),
          Dense(200 => 120),
          Dense(120 => 84),
          Dense(84 => 10)) |> gpu
```

We will use a crossentropy loss and an `Momentum` optimiser here. Crossentropy will be a good option when it comes to working with mulitple independent classes. Momentum gradually lowers the learning rate as we proceed with the training. It helps maintain a bit of adaptivity in our optimisation, preventing us from over shooting from our desired destination.

```julia
using Flux: logitcrossentropy, Momentum

loss(m, x, y) = logitcrossentropy(m(x), y)
opt_state = Flux.setup(Momentum(0.01), model)
```

We can start writing our train loop where we will keep track of some basic accuracy numbers about our model. We can define an `accuracy` function for it like so:

```julia
function accuracy(model, loader)
    n = 0
    acc = 0
    for batch in loader
        x, y = batch |> gpu
        ŷ = model(x)
        acc += sum(onecold(ŷ) .== onecold(y))
        n += numobs(x)
    end
    return acc / n
end
```

### Training the Classifier


Training is where we do a bunch of the interesting operations we defined earlier, and see what our net is capable of. We will loop over the dataset 10 times and feed the inputs to the neural network and optimise.

```julia
epochs = 10

for epoch in 1:epochs
    for batch in trainloader
        x, y = batch |> gpu
        g = gradient(model) do m 
            loss(m, x, y)
        end[1]
        Flux.update!(opt_state, model, g)
    end
    @show accuracy(model, valloader)
end
```

Seeing our training routine unfold gives us an idea of how the network learnt the function. This is not bad for a small hand-written network, trained for a limited time.

### Training on a GPU

The `gpu` functions you see sprinkled through this bit of the code tell Flux to move these entities to an available GPU, and subsequently train on it. No extra faffing about required! The same bit of code would work on any hardware with some small annotations like you saw here.

If you're not using `CUDA`, be sure to [configure Flux.jl for your GPU backend](https://fluxml.ai/Flux.jl/stable/gpu/#Selecting-GPU-backend).

### Testing the Network

We have trained the network for 100 passes over the training dataset. But we need to check if the network has learnt anything at all.

We will check this by predicting the class label that the neural network outputs, and checking it against the ground-truth. If the prediction is correct, we add the sample to the list of correct predictions. This will be done on a yet unseen section of data.

Okay, first step. Let us perform the exact same preprocessing on this set, as we did on our training set.

```julia
test_x, test_y = CIFAR10(:test)[:]
test_labels = onehotbatch(test_y, 0:9)
testloader = DataLoader((test_x, test_labels), batchsize = 1000, shuffle = true)
```

Next, display an image from the test set.

```julia
plot(image(test_x[:,:,:,rand(1:end)]))
```

The outputs of the networks are (log)likelihoods for the 10 classes. Higher the energy for a class, the more the network thinks that the image is of the particular class. Every column corresponds to the output of one image, with the 10 floats in the column being the energies.

Let's see how the model fared.

```julia
ids = rand(1:10000, 5)
rand_test = test_x[:,:,:,ids] |> gpu
rand_truth = test_y[ids]
model(rand_test)
```

This looks similar to how we would expect the results to be. At this point, it's a good idea to see how our net actually performs on new data, that we have prepared.

```julia
accuracy(model, testloader)
```

This is much better than random chance set at 10% (since we only have 10 classes), and not bad at all for a small hand written network like ours.

Let's take a look at how the net performed on all the classes performed individually.

```julia
confusion_matrix = zeros(Int, 10, 10)
m = model |> cpu
for batch in testloader
    @show numobs(batch)
    x, y = batch
    preds = m(x)
    ŷ = onecold(preds)
    y = onecold(y)
    for (yi, ŷi) in zip(y, ŷ)
        confusion_matrix[yi, ŷi] += 1
    end
end

confusion_matrix
```

The spread seems pretty good, with certain classes performing significantly better than the others. Why should that be?

!!! info
    Originally published at [fluxml.ai](https://fluxml.ai/tutorials/) on 15 November 2020.
    Written by Saswat Das, Mike Innes, Andrew Dinhobl, Ygor Canalli, Sudhanshu Agrawal, João Felipe Santos.

